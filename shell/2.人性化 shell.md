# 人性化 Shell

虽然 Bash Shell 是目前使用最广泛的 Shell，但它对人类不是最友好的。它从20世纪80年代末就开始存在了。在个人使用范围内，我强烈建议你使用一些现代的、对人友好的shell来替代Bash。不知道读者有没有使用过智能ABC等早期输入法，现代 Shell 就好像把智能ABC输入法替换成现在的搜狗输入法。

## 1 Fish Shell

Fish shell 将自己描述为一个智能的、用户友好的命令行 Shell。让我们先看看一些基本的用法，然后再进入配置主题。

### 1.1 安装

```shell
wget https://download.opensuse.org/repositories/shells:/fish:/release:/3/CentOS_7/x86_64/fish-3.4.1-1.1.x86_64.rpm
yum install fish-3.4.1-1.1.x86_64.rpm
chsh -s /usr/bin/fish
```

### 1.2 基本用法

对于许多日常工作来说，你不会注意到与Bash在输入方面有什么大的区别，表3-2中提供的大多数命令都是有效的。然而，有两个方面fish是不同的，而且比bash要方便得多。

- 没有明确的历史管理。你只需简单地输入，就能得到一个命令的先前执行情况。你可以用上下键来选择一个。

- 对许多命令有自动提示功能。此外，当你按下Tab键时，Fish shell会尝试完成命令、参数或路径，给你视觉上的提示，如在不能识别命令时将你的输入涂成红色。

在下表中，列出了一些常见的 fish 命令，在这种情况下，要特别注意对环境变量的处理。

| 任务                           | 命令                                  |
| ------------------------------ | ------------------------------------- |
| 导出环境变量 KEY 的值 VAL      | `set -x KEY VAL`                      |
| 删除环境变量 KEY               | `set -e KEY`                          |
| 内联环境变量 KEY，用于命令 cmd | `env KEY=VAL cmd`                     |
| 将路径长度改为1                | `set -g fish_prompt_pwd_dir_length 1` |
| 管理缩略语                     | `abbr`                                |
| 管理功能                       | `functions and funcd`                 |

与其他 Shell 不同，fish 将最后一个命令的退出状态存储在名为 $status 的变量中，而不是存储在 $？中。
如果你更熟悉 Bash，你可能还想查阅 [Fish FAQ](https://fishshell.com/docs/current/faq.html)，它解决了大多数问题。



### 1.3 配置

要[配置](https://fishshell.com/docs/current/index.html#configuration) Fish shell，你只需输入`fish_config` 命令（你可能需要添加浏览子命令，这取决于你的发行版），Fish 将通过 http://localhost:8000，并自动打开你的默认浏览器，其花哨的用户界面如图3-6所示，允许你查看和改变设置。

提示 ：为了在命令行导航的 vi 和 Emacs（默认）键绑定之间进行切换，使用`fish_vi_key_bindings`来启动vi模式，并使用`fish_default_key_bindings`将其重置为Emacs。注意，这些变化将立即在所有活动的shell会话中发生。

以下是我的配置，我的配置相当短，在`config.fish`中，我有以下内容：

```shell
set -x FZF_DEFAULT_OPTS "-m --bind='ctrl-o:execute(nvim {})+abort'"
set -x FZF_DEFAULT_COMMAND 'rg --files'
set -g FZF_CTRL_T_COMMAND "command find -L \$dir -type f 2> /dev/null | sed '1d; s#^\./##'"
set -x EDITOR nvim
set -x KUBE_EDITOR nvim
set -ga fish_user_paths /usr/local/bin
```

我的提示，定义在`fish_prompt.fish`中，如下：

```shell
function fish_prompt
    set -l retc red
    test $status = 0; and set retc blue

    set -q __fish_git_prompt_showupstream
    or set -g __fish_git_prompt_showupstream auto

    function _nim_prompt_wrapper
        set retc $argv[1]
        set field_name $argv[2]
        set field_value $argv[3]

        set_color normal
        set_color $retc
        echo -n '─'
        set_color -o blue
        echo -n '['
        set_color normal
        test -n $field_name
        and echo -n $field_name:
        set_color $retc
        echo -n $field_value
        set_color -o blue
        echo -n ']'
    end

    set_color $retc
    echo -n '┬─'
    set_color -o blue
    echo -n [
    set_color normal
    set_color c07933
    echo -n (prompt_pwd)
    set_color -o blue
    echo -n ']'
     # Virtual Environment
    set -q VIRTUAL_ENV_DISABLE_PROMPT
    or set -g VIRTUAL_ENV_DISABLE_PROMPT true
    set -q VIRTUAL_ENV
    and _nim_prompt_wrapper $retc V (basename "$VIRTUAL_ENV")

    # git
    set prompt_git (fish_git_prompt | string trim -c ' ()')
    test -n "$prompt_git"
    and _nim_prompt_wrapper $retc G $prompt_git

    # New line
    echo

    # Background jobs
    set_color normal
    for job in (jobs)
        set_color $retc
        echo -n '│ '
        set_color brown
        echo $job
    end
    set_color blue
    echo -n '╰─> '
        set_color -o blue
    echo -n '$ '
    set_color normal
end
```



